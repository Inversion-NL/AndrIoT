apply plugin: 'checkstyle'
apply plugin: 'findbugs'

def isJava = plugins.hasPlugin('java')
// Add checkstyle, findbugs and lint to the check task.
// Comment it if already set in root project's build.gradle.
check.dependsOn 'checkstyle', 'findbugs'
if (!isJava) {
    check.dependsOn 'lint'
}

checkstyle {
    toolVersion '7.8.2'

    configFile file("${project.rootDir}/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionFilterPath = file(
            "${project.rootDir}/quality/checkstyle/suppressions.xml")
            .absolutePath
}

task checkstyle(type: Checkstyle, group: 'verification') {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    classpath = files()
}


findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/quality/findbugs/findbugs-filter.xml")
}

task findbugs(type: FindBugs, dependsOn: "assemble", group: 'verification') {
    if (isJava) {
        classes = files("$projectDir.absolutePath/build/classes")
    } else {
        classes = files("$projectDir.absolutePath/build/intermediates/classes")
    }
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        // FindBugs tasks can only have one report enabled
        xml.enabled = false
        html.enabled = !xml.enabled
        xml {
            destination new File("$projectDir.absolutePath/build/reports/findbugs/findbugs.xml")
        }
        html {
            destination new File("$projectDir.absolutePath/build/reports/findbugs/findbugs.html")
        }
    }

    classpath = files()
}

if (!isJava) {
    android {
        lintOptions {
            disable 'NewApi' // gradle build will compile code use `Objects.requireNonNull()` for lambda
            abortOnError true
            xmlReport true
            xmlOutput project.file('build/reports/lint/lint-report.xml')
            htmlReport true
            htmlOutput project.file('build/reports/lint/lint-report.html')
            lintConfig file("${project.rootDir}/quality/lint/lint.xml")
        }

        testOptions.unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            jvmArgs '-noverify'
        }
    }
}

apply from: rootProject.file('quality/jacoco.gradle')