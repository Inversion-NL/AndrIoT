apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 2
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion rootProject.ext.compile_sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        applicationId "com.rosterloh.andriot"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    dataBinding.enabled true
}

repositories {
    maven { url "https://repo.eclipse.org/content/repositories/paho-snapshots/" }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation(name: 'tensorflow', ext: 'aar')

    compileOnly "com.google.android.things:androidthings:$things_version"
    compileOnly "com.google.dagger:dagger:$dagger_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.google.android.gms:play-services-nearby:$play_services_version"
    implementation "com.google.firebase:firebase-crash:$play_services_version"
    implementation "android.arch.persistence.room:runtime:$arch_version"
    implementation "android.arch.lifecycle:runtime:$arch_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$mqtt_version"
    implementation "com.jakewharton.timber:timber:$timber_version"
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"
    implementation 'io.jsonwebtoken:jjwt:0.7.0'

    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "android.arch.persistence.room:compiler:$arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'