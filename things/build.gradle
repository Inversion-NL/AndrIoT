apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 1
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion rootProject.ext.compile_sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        applicationId "com.rosterloh.andriot"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    dataBinding {
        enabled = true
    }
}

repositories {
    maven { url "https://repo.eclipse.org/content/repositories/paho-snapshots/" }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile project(':common')
    compile(name: 'tensorflow', ext: 'aar')

    provided "com.google.android.things:androidthings:$things_version"
    compile 'io.mraa.at.upm:upm_htu21d:1.3.0'

    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    compile "com.android.support:design:$support_version"
    compile "com.google.android.gms:play-services-location:$play_services_version"
    compile "com.google.firebase:firebase-crash:$play_services_version"
    compile "android.arch.persistence.room:runtime:$arch_version"
    compile "android.arch.lifecycle:runtime:$arch_version"
    compile "android.arch.lifecycle:extensions:$arch_version"
    compile "com.google.dagger:dagger:$dagger_version"
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile ("org.eclipse.paho:org.eclipse.paho.client.mqttv3:$mqtt_version", {
        exclude group: 'com.google.android', module: 'support-v4'
    })
    compile ("org.eclipse.paho:org.eclipse.paho.android.service:$mqtt_version", {
        exclude group: 'com.google.android', module: 'support-v4'
    })
    compile "com.jakewharton.timber:timber:$timber_version"
    compile "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"

    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "android.arch.persistence.room:compiler:$arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"

    testCompile "junit:junit:$junit_version"
    testCompile ("android.arch.core:core-testing:$arch_version", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    androidTestCompile "com.android.support:appcompat-v7:$support_version"
    androidTestCompile "com.android.support:support-v4:$support_version"
    androidTestCompile "com.android.support:design:$support_version"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestCompile("android.arch.core:core-testing:$arch_version", {
    })
    androidTestCompile "org.mockito:mockito-android:$mockito_version"
}

apply plugin: 'com.google.gms.google-services'